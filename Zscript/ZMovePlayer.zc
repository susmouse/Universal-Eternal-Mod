enum Bobbing
{
	Bob_Normal,
	Bob_Inverse,
	Bob_Alpha,
	Bob_InverseAlpha,
	Bob_Smooth,
	Bob_InverseSmooth,
	Bob_Build,
	Bob_Dusk,
	Bob_Painkiller,
	Bob_UT
}

Class BuildPlayer : PlayerPawn
{
	//=========================
	//Common
	
	const BOB_MIN_REALIGN 	 = 0.25f;
	const GROUND_DASH_COOLER = 18;
	
	//Movement General
	bool	Pain;
	double	ViewAngleDelta;
	float	ActualSpeed;
	float	MaxAirSpeed;
	float 	MaxGroundSpeed;
	float	MoveFactor;
	int		AnimateJump;
	int		ForceVelocity;
	int		OldFloorZ;
	playerinfo ZMPlayer;
	vector2 OldVelXY;
	vector3	Acceleration;
	
	//////////////////
	
	//Jumping
	bool 	BlockJump;
	bool	Jumped;
	float	FloorAngle;
	int		DoubleJumpCooler;
	int		JumpSoundCooler;
	
	//Double Jump
	bool	BlockDoubleJump;
	bool	CanDoubleJump;
	
	//Elevator Jumps
	float	ElevatorJumpBoost;
	int		OldSecIndex;
	
	//////////////////
	
	//Double Tap
	int		FirstTapTime;
	int		FirstTapValue;
	int		OldTapValue;
	
	//Dashing
	float	WJumpSpeed;
	int		DashNumber;
	int		DashCooler;
	
	//Air Dashing
	float	ADashTargetSpeed;
	int		ADashCooler;
	int		ADashFrictionDelay;
	
	//WallJump
	int		CheckForWJump;
	
	//WallSlideMove
	bool	CanWSlide;
	int		CheckForWSlide;
	vector2	WSlideVelocity;
	
	//Crouch Slide
	bool	CanCSlide;
	float	MaximumSlideSpeed;
	float	QSlideDuration;
	int		CSlideStartTime;
	
	//Ledge Grabbing
	bool	LedgeGrabbed;
	float	LedgeAngle;
	int		LedgeHeight;
	int		LedgeTime;
	weapon	ReselectWeapon;
	
	//Grappling Hook
	actor	GrappledMonster;
	actor	HookFired;
	bool	Grappled;
	float	PendulumLength;
	vector3	GrappleVel;
	vector3 Rope;
	
	//////////////////
	
	//View Bobbing
	bool	PostLandingBob;
	float	ZMBob;
	
	//Weapon bobbing
	bool	DoBob;
	double	BobTime;
	double	HorizontalSway;
	double	BobRange;
	double 	OldTicFrac;
	double	VerticalOffset;
	
	//=========================
	//Build Engine Only
	
	//Movement
	bool	DeepWater;
	float	LandingVelZ;
	int		FVel;
	int		SVel;
	int		UVel;
	
	//Jumping
	int		BuildJumpDelay;
	
	
	
	bool	zm_wallfriction;
	property zm_wallfriction : zm_wallfriction;
	
	bool	zm_dropprevention;
	property zm_dropprevention : zm_dropprevention;
	
	float 	zm_friction;
	property zm_friction : zm_friction;
	
	float	zm_maxgroundspeed;
	property zm_maxgroundspeed : zm_maxgroundspeed;
	
	float	zm_walkspeed;
	property zm_walkspeed : zm_walkspeed;
	
	float	zm_crouchspeed;
	property zm_crouchspeed : zm_crouchspeed;
	
	float	zm_strafemodifier;
	property zm_strafemodifier : zm_strafemodifier;
	
	float	zm_jumpheight;
	property zm_jumpheight : zm_jumpheight;
	
	float	zm_setgravity;
	property zm_setgravity : zm_setgravity;
	
	bool	zm_autojump;
	property zm_autojump : zm_autojump;
	
	int 	zm_doublejump;
	property zm_doublejump : zm_doublejump;
	
	float 	zm_doublejumpheight;
	property zm_doublejumpheight : zm_doublejumpheight;
	
	float 	zm_rjumpmulti;
	property zm_rjumpmulti : zm_rjumpmulti;
	
	bool 	zm_elevatorjump;
	property zm_elevatorjump : zm_elevatorjump;
	
	float 	zm_ejumpmultiplier;
	property zm_ejumpmultiplier : zm_ejumpmultiplier;
	
	float	zm_landingspeed;
	property zm_landingspeed : zm_landingspeed;
	
	float	zm_minlanding;
	property zm_minlanding : zm_minlanding;
	
	float	be_jumpanim;
	property be_jumpanim : be_jumpanim;
	
	int		zm_crouchslide;
	property zm_crouchslide : zm_crouchslide;
	
	bool	zm_sway;
	property zm_sway : zm_sway;
	
	float	zm_swayspeed;
	property zm_swayspeed : zm_swayspeed;
	
	float	zm_swayrange;
	property zm_swayrange : zm_swayrange;
	
	bool	zm_swaydirection;
	property zm_swaydirection : zm_swaydirection;
	
	bool	zm_offset;
	property zm_offset : zm_offset;
	
	float	zm_offsetspeed;
	property zm_offsetspeed : zm_offsetspeed;
	
	int		zm_offsetrange;
	property zm_offsetrange : zm_offsetrange;
	
	bool	zm_offsetdirection;
	property zm_offsetdirection : zm_offsetdirection;
	
	
	Default
    {
		BuildPlayer.zm_wallfriction			 false;
		BuildPlayer.zm_dropprevention		 false;
		BuildPlayer.zm_friction				 7.0;
		BuildPlayer.zm_maxgroundspeed		 15.0;//15.0;
		BuildPlayer.zm_walkspeed			 1.0;
		BuildPlayer.zm_crouchspeed			 0.7;
		BuildPlayer.zm_strafemodifier		 1.0;
		BuildPlayer.zm_jumpheight			 8.0;
		BuildPlayer.zm_setgravity			 0.75;
		BuildPlayer.zm_autojump				 false;
		BuildPlayer.zm_doublejump			 0;
		BuildPlayer.zm_doublejumpheight		 1.2;
		BuildPlayer.zm_rjumpmulti			 0.7;
		BuildPlayer.zm_elevatorjump			 false;
		BuildPlayer.zm_ejumpmultiplier		 1.0;
		BuildPlayer.zm_landingspeed			 0.25;
		BuildPlayer.zm_minlanding			 0.5;
		BuildPlayer.be_jumpanim				 0;//6.0;
		BuildPlayer.zm_crouchslide			 0;
		
		BuildPlayer.zm_sway					 false;
		BuildPlayer.zm_swayspeed			 2;
		BuildPlayer.zm_swayrange			 25;
		BuildPlayer.zm_swaydirection		 false;
		
		BuildPlayer.zm_offset				 false;
		BuildPlayer.zm_offsetspeed			 2;
		BuildPlayer.zm_offsetrange			 25;
		BuildPlayer.zm_offsetdirection		 false;
	
	
		Player.DisplayName "Build Player";
        PainChance 255;
    }
	
	////////////////////////////////////////////////////////////////////////////////////////////////
	////////////////////////////////////////////////////////////////////////////////////////////////
	////																						////
	//// Non-Movement Stuff																		////
	////																						////
	////////////////////////////////////////////////////////////////////////////////////////////////
	////////////////////////////////////////////////////////////////////////////////////////////////
	
	//Because GZDoom's Unit() returns NaN if a vector has no value
	vector3 SafeUnit3(Vector3 VecToUnit)
	{
		if(VecToUnit.Length()) { VecToUnit /= VecToUnit.Length(); }
		return VecToUnit;
	}
	
	vector2 SafeUnit2(Vector2 VecToUnit)
	{
		if(VecToUnit.Length()) { VecToUnit /= VecToUnit.Length(); }
		return VecToUnit;
	}
	
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		//No voodoo dolls allowed past this point
		if(!self.player || self.player.mo != self) { return; }
		bNOFRICTION = True;
	}
	
	UEM_DEDashJump DashMove;
	UEM_DEDashJump DED_control;
	
	Override void PlayerThink()
	{
		//======================================
		//Store info needed in multiple places
		
		if (!DED_control)
			DED_control = UEM_DEDashJump(FindInventory("UEM_DEDashJump"));
		
		
		if (DED_control)
			{
				if	(!DED_control.DEDash)	
				{	
					MaxGroundSpeed = zm_maxgroundspeed * ActualSpeed;	
				}
				
				Else 						
				{	
					MaxGroundSpeed = 100;	
				}
			}
		/*
		Else
			{	
				MaxGroundSpeed = zm_maxgroundspeed * ActualSpeed;	
			}
		*/
		
		ZMPlayer = self.player;
		ActualSpeed = Speed * GetPowerSpeed();
		//MaxGroundSpeed = zm_maxgroundspeed * ActualSpeed;
		MoveFactor = ScaleMovement();
		//MoveType = zm_movetype;
		Pain = InStateSequence(CurState, FindState("Pain"));
		ZMPlayer.OnGround = Pos.Z <= FloorZ || bONMOBJ || bMBFBOUNCER || (ZMPlayer.Cheats & CF_NOCLIP2);
		
		/*
		zm_wallfriction			= false;
		zm_dropprevention		= false;
		zm_friction				= 7.0;
		zm_maxgroundspeed		= 15.0;//15.0;
		zm_walkspeed			= 1.0;
		zm_crouchspeed			= 0.7;
		zm_strafemodifier		= 1.0;
		zm_jumpheight			= 8.0;
		zm_setgravity			= 0.75;
		zm_autojump				= false;
		zm_doublejump			= 0;
		zm_doublejumpheight		= 1.2;
		zm_rjumpmulti			= 0.7;
		zm_elevatorjump			= false;
		zm_ejumpmultiplier		= 1.0;
		zm_landingspeed			= 0.25;
		zm_minlanding			= 0.5;
		be_jumpanim				= 0;//6.0;
		zm_crouchslide			= 0;
		
		zm_sway					= false;
		zm_swayspeed			= 2;
		zm_swayrange			= 25;
		zm_swaydirection		= false;
		
		zm_offset				= false;
		zm_offsetspeed			= 2;
		zm_offsetrange			= 25;
		zm_offsetdirection		= false;
		*/
		
			
		
		//======================================
		//Execute Player tic cycle
		
		CheckFOV();
		
		if(ZMPlayer.inventorytics) { ZMPlayer.inventorytics--; }
		CheckCheats();

		bool totallyfrozen = CheckFrozen();

		// Handle crouching
		CheckCrouch(totallyfrozen);
		CheckMusicChange();

		if(ZMPlayer.playerstate == PST_DEAD)
		{
			DeathThink();
			return;
		}
		if(ZMPlayer.morphTics && !(ZMPlayer.cheats & CF_PREDICTING)) { MorphPlayerThink (); }

		CheckPitch();
		HandleMovement();
		CalcHeight();

		if(!(ZMPlayer.cheats & CF_PREDICTING))
		{
			CheckEnvironment();
			// Note that after this point the PlayerPawn may have changed due to getting unmorphed or getting its skull popped so 'self' is no longer safe to use.
			// This also must not read mo into a local variable because several functions in this block can change the attached PlayerPawn.
			ZMPlayer.mo.CheckUse();
			ZMPlayer.mo.CheckUndoMorph();
			// Cycle psprites.
			ZMPlayer.mo.TickPSprites();
			// Other Counters
			if(ZMPlayer.damagecount) ZMPlayer.damagecount--;
			if(ZMPlayer.bonuscount) ZMPlayer.bonuscount--;

			if(ZMPlayer.hazardcount)
			{
				ZMPlayer.hazardcount--;
				if(!(Level.maptime % ZMPlayer.hazardinterval) && ZMPlayer.hazardcount > 16*TICRATE)
					ZMPlayer.mo.DamageMobj (NULL, NULL, 5, ZMPlayer.hazardtype);
			}
			ZMPlayer.mo.CheckPoison();
			ZMPlayer.mo.CheckDegeneration();
			ZMPlayer.mo.CheckAirSupply();
		}
		
		//Bob weapon stuff
		BobWeaponAuxiliary();
		
		//Old values for comparisons
		OldFloorZ = FloorZ;
		OldVelXY = Vel.XY;
	}
	
	float GetPowerSpeed()
	{
		float factor = 1.f;
		
		if(!ZMPlayer.morphTics)
		{
			for(let it = Inv; it != null; it = it.Inv)
			{
				factor *= it.GetSpeedFactor();
			}
		}
		
		return factor;
	}
	
	Override void DeathThink()
	{
		bNOFRICTION = False;
		Gravity = zm_setgravity;
		
		Super.DeathThink();
	}
	
	Override void CalcHeight()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		double HeightAngle;
		double bob;
		bool still = false;

		if(!ZMPlayer.OnGround || (ZMPlayer.OnGround && ((cmd.buttons & BT_JUMP) && !BlockJump)) || ZMPlayer.cheats & CF_NOCLIP2) //nobody walks in the air
		{
			ZMBob--;
			ZMBob = max(bNOGRAVITY ? 0.5f : 0.f, ZMBob);
			PostLandingBob = True;
		}
		else
		{
			if(PostLandingBob)
			{
				ZMBob += Vel.XY.Length() / (MaxGroundSpeed ? MaxGroundSpeed : 1.f);
				if(ZMBob >= Vel.XY.Length() * ZMPlayer.GetMoveBob()) { PostLandingBob = False; }
			}
			else
			{
				ZMBob = Vel.XY.Length() * ZMPlayer.GetMoveBob(); //this way all GetMoveBob() values are meaningful
			}
			
			if(!ZMBob)
				still = true;
			else
				ZMBob = min(MaxGroundSpeed, ZMBob);
		}

		double defaultviewheight = ViewHeight + ZMPlayer.crouchviewdelta;

		if(ZMPlayer.cheats & CF_NOVELOCITY)
		{
			ZMPlayer.viewz = pos.Z + defaultviewheight;

			if(ZMPlayer.viewz > ceilingz-4)
				ZMPlayer.viewz = ceilingz-4;

			return;
		}

		if(still)
		{
			if(ZMPlayer.health > 0)
				bob = 2 * ZMPlayer.GetStillBob() * sin(2 * Level.maptime);
			else
				bob = 0;
		}
		else
		{
			HeightAngle = Level.maptime / 20. * 360.;
			bob = ZMBob * sin(HeightAngle) * (waterlevel > 2 ? 0.25f : 0.5f);
		}
		
		if(ZMPlayer.morphTics) { bob = 0; }
		
		//=======================================
		// Customizable Landing
		
		
		if(ZMPlayer.playerstate == PST_LIVE)
		{
			if(!ZMPlayer.OnGround)
			{
				if(Vel.Z >= 0)
				{
					ZMPlayer.viewheight += ZMPlayer.deltaviewheight;
					ZMPlayer.deltaviewheight += zm_landingspeed * 2.f; //ensure a speedy recovery while in the air
					if(ZMPlayer.viewheight >= defaultviewheight)
					{
						ZMPlayer.deltaviewheight = 0;
						ZMPlayer.viewheight = defaultviewheight;
					}
				}
				else
				{
					//LandingVelZ = abs(Vel.Z);
					//ZMPlayer.deltaviewheight = Vel.Z / zm_landingsens;
					ZMPlayer.viewheight = defaultviewheight;
				}
			}
			else
			{
				ZMPlayer.viewheight += ZMPlayer.deltaviewheight;

				if(ZMPlayer.viewheight > defaultviewheight)
				{
					ZMPlayer.viewheight = defaultviewheight;
					ZMPlayer.deltaviewheight = 0;
				}
				else if(ZMPlayer.viewheight < defaultviewheight * zm_minlanding && !BuildJumpDelay)
				{
					ZMPlayer.viewheight = defaultviewheight * zm_minlanding;
					if(ZMPlayer.deltaviewheight <= 0) { ZMPlayer.deltaviewheight = 1 / 65536.f; }
				}
				
				if(ZMPlayer.deltaviewheight)	
				{
					ZMPlayer.deltaviewheight += zm_landingspeed;
					if(!ZMPlayer.deltaviewheight) { ZMPlayer.deltaviewheight = 1 / 65536.f; }
				}
			}
		}
			
			
		//Let's highlight the important stuff shall we?
		ZMPlayer.viewz = pos.Z + ZMPlayer.viewheight + (bob * clamp(ViewBob, 0., 1.5));
		
		if(Floorclip && ZMPlayer.playerstate != PST_DEAD && pos.Z <= floorz) { ZMPlayer.viewz -= Floorclip; }
		if(ZMPlayer.viewz > ceilingz - 4) { ZMPlayer.viewz = ceilingz - 4; }
		if(ZMPlayer.viewz < FloorZ + 4) { ZMPlayer.viewz = FloorZ + 4; }
	}
	
	Override void CheckPitch()
	{
		int clook = ZMPlayer.cmd.pitch;
		if(clook != 0)
		{
			if(clook == -32768)
			{
				ZMPlayer.centering = true;
			}
			else if(!ZMPlayer.centering)
			{
				A_SetPitch(Pitch - clook * (360. / 65536.), SPF_INTERPOLATE);
			}
		}
		
		if(ZMPlayer.centering)
		{
			if(abs(Pitch) > 2.)
			{
				Pitch *= (2. / 3.);
			}
			else
			{
				Pitch = 0.;
				ZMPlayer.centering = false;
				if(PlayerNumber() == consoleplayer)
				{
					LocalViewPitch = 0;
				}
			}
		}
	}
	
	////////////////////////////////////////////////////////////////////////////////////////////////
	////////////////////////////////////////////////////////////////////////////////////////////////
	////																						////
	//// Movement Stuff																			////
	////																						////
	////////////////////////////////////////////////////////////////////////////////////////////////
	////////////////////////////////////////////////////////////////////////////////////////////////
	
	Override void HandleMovement()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		// [RH] Check for fast turn around
		if(cmd.buttons & BT_TURN180 && !(ZMPlayer.oldbuttons & BT_TURN180)) { ZMPlayer.turnticks = TURN180_TICKS; }

		// Handle movement
		if(reactiontime)
		{ // Player is frozen
			reactiontime--;
		}
		else
		{	
			ViewAngleDelta = cmd.Yaw * (360.0 / 65536.0); //needed for two other things
			
			if(ZMPlayer.TurnTicks) //moved here to save many doubled lines
			{
				ZMPlayer.TurnTicks--;
				A_SetAngle(Angle + (180.0 / TURN180_TICKS), SPF_INTERPOLATE);
			}
			else
			{
				A_SetAngle(Angle + ViewAngleDelta, SPF_INTERPOLATE);
				if(LedgeGrabbed) { Angle = clamp(Angle, LedgeAngle - 20, LedgeAngle + 20); }
			}
			
			//========================================
			//Acrobatics triggers
			//if(CVar.GetCVar("zm_doubletap", Player).GetBool()) { DoubleTapCheck(); }
			//Dashing parameters reset
			if(DashCooler)
			{
				if(DashCooler <= GROUND_DASH_COOLER || DashCooler > 100)
				{
					DashCooler--;
				}
				else if(ZMPlayer.OnGround || WaterLevel >= 2 || bNOGRAVITY)
				{
					WJumpSpeed = DashNumber = 0;
					DashCooler = GROUND_DASH_COOLER;
				}
			}
			//Air Dash
			/*
			if(zm_airdash || zm_wjump)
			{
				if(ADashCooler) { ADashCooler--; }
				if(ADashTargetSpeed) { AirDashFriction(); }
			}
			*/
			//Wall Jumping
			//if(CheckForWJump) { DashInitiator(); }
			//Wall Slide
			//if(CheckForWSlide) { WallSlideInitiator(); }
			//Crouchsliding
			//if(zm_crouchslide) { CSlideInitiator(); }
			
			//========================================
			//Gravity
			BuildGravity();
			
			//========================================
			//Say no to wall friction
			QuakeWallFriction();
			
			//========================================
			//Actual Movement selection
			/*
			if(CanWSlide)
			{
				WallSlideMove();
			}/*
			else if(LedgeGrabbed) //DEDash
			{
				LedgeGrab();
			}*/
			if(WaterLevel >= 2)
			{
				BuildWaterMove();
				//if(zm_ledgegrab) { LedgeGrabInitiator(); }
			}
			else if(bNOGRAVITY)
			{
				BuildFlyMove();
			}
			/*
			else if(!DEDash)
			{
				MidDashMove();
			}
			
			/*
			else if(CanCSlide)
			{
				if(zm_crouchslide == 2)
					QSlideMove();
				else
					CSlideMove();
				
				LandingVelZ = 0.f;
			}
			*/
			
			else
			{
				//if(!DEDash)
				//{
				//MidDashMove();
				//}
				//else
				//{
				BuildHandleMove();
				//}
			}
			
			//========================================
			//Jumping
			BuildJump();
			
			//========================================
			//Misc
			if(ZMPlayer.Cheats & CF_REVERTPLEASE != 0)
			{
				ZMPlayer.Cheats &= ~CF_REVERTPLEASE;
				ZMPlayer.Camera = ZMPlayer.Mo;
			}
			
			CheckMoveUpDown();
		}
	}
	
	void QuakeWallFriction()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		if(ForceVelocity)
		{
			if(OldVelXY.Length() && Vel.XY.Length() && (cmd.forwardmove || cmd.sidemove))
			{
				Vector2 VelUnit = Vel.XY.Unit();
				Float VelDot = OldVelXY.Unit() dot VelUnit;
				if(VelDot > 0)
				{
				   if(VelDot > 0.75)
					  VelDot = 1.f;
				   else
					  VelDot /= 0.75;
				   
				   Vel.XY = VelDot * OldVelXY.Length() * VelUnit;
				}
				ForceVelocity--;
			}
			else
			{
				ForceVelocity = 0;
			}
		}
		if(!CheckMove(Pos.XY + Vel.XY)) { ForceVelocity = 2; }
	}
	
	//////////////////////////////////////////
	// Jumping								//
	//////////////////////////////////////////
	
	void PreJumpCommon()
	{
		//Jumptics settings
		if(Jumped && (Player.OnGround || WaterLevel >= 2 || bNOGRAVITY)) { Jumped = False; }
		
		//Jump Sound Cooler
		if(JumpSoundCooler) { JumpSoundCooler--; }
		
		//Doudle jump cooler
		if(DoubleJumpCooler) { DoubleJumpCooler--; }
		
		//Double Jump
		if(ZMPlayer.OnGround) { CanDoubleJump = True; }
	}
	
	float GetPowerJump()
	{
		Float JumpFac = 0.f;
		
		for(let p = Inv; p != null; p = p.Inv)
		{
			let pp = PowerHighJump(p);
			if(pp) { JumpFac = max(JumpFac, pp.Strength); }
		}
		
		return JumpFac;
	}
	
	bool, bool CheckIfJumpable()
	{
		if(CeilingZ - FloorZ <= Height) //sector is just high enough for player to pass through but not jump
		{
			return True, False;
		}
		else
		{
			//===============================
			// Get floor normal
			
			Vector3 FloorNormal;
			F3DFloor ThreeDFloor;
			for(int i = 0; i < FloorSector.Get3DFloorCount(); i++)
			{
				if(FloorSector.Get3DFloor(i).Top.ZAtPoint(Pos.XY) ~== FloorZ)
				{
					ThreeDFloor = FloorSector.Get3DFloor(i);
					break;
				}
			}
			FloorNormal = ThreeDFloor ? - ThreeDFloor.Top.Normal : FloorSector.FloorPlane.Normal;
			FloorAngle = atan2(FloorNormal.XY.Length(), FloorNormal.Z);
			
			//==============================
			//Come to the logical conclusion
			
			if(FloorAngle < 45)
				return BlockJump ? True : False, !FloorAngle ? False : True;
			else
				return ZMPlayer.OnGround ? True : False, True; //floor is too steep
		}
    }
	
	void ElevatorJump(bool SlopedFloor)
	{
		Int SecIndex = FloorSector.Index();
		Bool CheckForElevator = ZMPlayer.OnGround && SecIndex == OldSecIndex && !SlopedFloor;
		
		if(zm_elevatorjump)
		{
			if(CheckForElevator && FloorZ > OldFloorZ) //no accidental elevator jump boost on slopes
				ElevatorJumpBoost = (FloorZ - OldFloorZ) * zm_ejumpmultiplier;
			else
				ElevatorJumpBoost = 0;
		}
		else
		{
			if(CheckForElevator && FloorZ - OldFloorZ >= zm_jumpheight / 2.f) { BlockJump = True; } //floor is raising too fast to not spam jump
			ElevatorJumpBoost = 0;
		}
		
		OldSecIndex = SecIndex;
	}
	
	void BuildGravity()
	{
		if(!bNOGRAVITY && !WaterLevel < 2 && !GrappleVel.Length())
			Gravity = zm_setgravity;
		else
			Gravity = 0.f;
	}
	
	void BuildJump()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		//Common stuff
		PreJumpCommon();
		
		//underwater/flying specific jump behavior are in WaterMove and FlyMove
		if(WaterLevel >= 2 || bNOGRAVITY) { return; }
		
		//Check slope angle and sector height
		Bool SlopedFloor;
		[BlockJump, SlopedFloor] = CheckIfJumpable();
		
		//Elevators Jump Boost
		ElevatorJump(SlopedFloor);
		
		////////////////////////////////
		//Actual Jump
		if(cmd.buttons & BT_JUMP || BuildJumpDelay)
		{
			//Special circumstances jump denial
			if(LedgeGrabbed || GrappleVel.Length() || CanWSlide || LandingVelZ >= 10.f || DashCooler > 100) { return; }
			
			if(ZMPlayer.crouchoffset != 0)
			{
				ZMPlayer.crouching = 1;
			}
			else if(ZMPlayer.onground && !BlockJump)
			{
				Double BuildSmallerJump;
				
				if(ZMPlayer.deltaviewheight)
				{
					BuildSmallerJump = 0.85f;
				}
				else
				{
					BuildJumpDelay++;
					if(BuildJumpDelay == 1)
					{
						ZMPlayer.viewheight -= be_jumpanim;
						return;
					}
					else
					{
						BuildJumpDelay = 0;
						BuildSmallerJump = 1.f;
					}
				}
				
				Float JumpVelZ = zm_jumpheight + ElevatorJumpBoost;
				Float JumpFac = GetPowerJump();
				if(JumpFac) { JumpVelZ *= JumpFac; }
				
				Vel.Z += (Vel.Z > 0 ? zm_rjumpmulti : 1) * JumpVelZ;
					
				bONMOBJ = false;
				Jumped = True;
					
				if(!(ZMPlayer.cheats & CF_PREDICTING) && !JumpSoundCooler)
				{
					A_StartSound("*jump", CHAN_BODY);
					JumpSoundCooler = 4;
				}
				
				BlockJump = zm_autojump ? False : True;
				DoubleJumpCooler = 5;
			}
			else if(!ZMPlayer.OnGround)
			{
				if(BuildJumpDelay) { BuildJumpDelay = 0; }
				
				if(CanDoubleJump && !BlockDoubleJump && !DoubleJumpCooler && ((zm_doublejump == 1 && Vel.Z > 0) || zm_doublejump == 2))
				{
					Float DoubleJumpVelZ = zm_jumpheight * zm_doublejumpheight;
					Float JumpFac = GetPowerJump();
					if(JumpFac) { DoubleJumpVelZ *= JumpFac; }
					Vel.Z = DoubleJumpVelZ;
					
					bONMOBJ = false;
					Jumped = True;
					
					if(!(ZMPlayer.cheats & CF_PREDICTING)) { A_StartSound("*jump", CHAN_BODY); }
					
					CanDoubleJump = False;
				}
			}
			
			BlockDoubleJump = True;
		}
		else
		{
			BlockDoubleJump = False;
			BlockJump = False;
		}
	}
	
	//////////////////////////////////////////
	// Ground Movement						//
	//////////////////////////////////////////
	
	float ScaleMovement()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		Float MoveMulti;
		if(cmd.sidemove || cmd.forwardmove)
		{
			Bool IsWalking = (CVar.GetCVar("cl_run", Player).GetBool() && (cmd.buttons & BT_SPEED)) || (!CVar.GetCVar("cl_run", Player).GetBool() && !(cmd.buttons & BT_SPEED));
			
			if(ZMPlayer.CrouchFactor == 0.5)
				MoveMulti = min(zm_crouchspeed, zm_walkspeed);
			else if(IsWalking)
				MoveMulti = zm_walkspeed;
			else
				MoveMulti = 1.5;//1.5;
		}
		else
		{
			MoveMulti = 1.5;//1.5;
		}
		
		return MoveMulti;
	}
	
	float SpeedMulti()
	{
		return MoveFactor * (ZMPlayer.cmd.forwardmove && ZMPlayer.cmd.sidemove ? zm_strafemodifier : 1);
	}
	
	float AccelMulti()
	{
		return ActualSpeed * (ZMPlayer.cmd.forwardmove && ZMPlayer.cmd.sidemove ? zm_strafemodifier : 1);
	}
	
	/*
	float SpeedMulti()
	{
		Usercmd cmd = ZMPlayer.cmd;
		return MoveFactor * (cmd.forwardmove && cmd.sidemove ? zm_strafemodifier : 1);
	}
	
	float AccelMulti()
	{
		Usercmd cmd = ZMPlayer.cmd;
		return ActualSpeed * (cmd.forwardmove && cmd.sidemove ? zm_strafemodifier : 1);
	}
	*/
	void DropPrevention()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		Bool GuardRail = ((!cmd.sidemove && !cmd.forwardmove && Vel.XY.Length()) ||
						 (CVar.GetCVar("cl_run", Player).GetBool() && (cmd.buttons & BT_SPEED)) || (!CVar.GetCVar("cl_run", Player).GetBool() && !(cmd.buttons & BT_SPEED))) //fuck me having to do this
						 && ZMPlayer.OnGround && !Pain;
		
		if(GuardRail)
		{
			Float GuardAngle = VectorAngle(Vel.X, Vel.Y);
			Float GuardX = Pos.X + Radius * cos(GuardAngle);
			Float GuardY = Pos.Y + Radius * sin(GuardAngle);
			Bool FallDanger = Pos.Z - GetZAt(GuardX, GuardY, 0, GZF_ABSOLUTEPOS|GZF_ABSOLUTEANG) > Height;
			if(FallDanger) { Vel.XY *= 0.5f; }
		}
	}
	
	void GroundSpriteAnimation()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		if(ZMPlayer.Cheats & CF_PREDICTING == 0 && Vel.XY.Length() > 1.f && (cmd.forwardmove || cmd.sidemove))
			PlayRunning();
		else
			PlayIdle();
			
		AnimateJump = 6;
	}
	
	void AirSpriteAnimation()
	{
		if(AnimateJump)
		{
			PlayRunning();
			AnimateJump--;
		}
		else
		{
			PlayIdle();
		}
	}
	
	//====================================
	
	void DFriction()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		//Going too slow, stop
		if(WaterLevel >= 2 || bNOGRAVITY)
		{
			if(Vel.Length() < 1.f && !cmd.sidemove && !cmd.forwardmove)
			{
				Vel.XY = (0, 0);
				return;
			}
		}
		else if(Vel.XY.Length() < 1.f && !cmd.sidemove && !cmd.forwardmove)
		{
			Vel.XY = (0, 0);
			return;
		}
		
		//Balance friction with inputs strength
		if(WaterLevel >= 2)
		{
			Acceleration *= 4.f;
			Vel *= 0.6f;
		}
		else if(bNOGRAVITY)
		{
			Vel *= 0.9f;
		}
		else
		{
			Acceleration.XY *= zm_friction;
			Vel.XY *= 1 - zm_friction / 10.f;
		}
	}
	
	void WaterVelZLimiter()
	{
		//Avoids dolphin jumping out of water
		Vel.Z = min(6.f, Vel.Z);
		if(Pitch < 0) { Acceleration.Z = 0; }
	}
	
	//====================================
	// Build Engine
	
	void BuildHandleMove()
	{
		MaxGroundSpeed *= SpeedMulti();
		
		if(!ZMPlayer.OnGround || (ZMPlayer.OnGround && DashNumber))
		{
			BuildAirMove();
			//if(zm_ledgegrab) { LedgeGrabInitiator(); }
		}
		else
		{
			BuildGroundMove();
			Grappled = False;
			if(zm_dropprevention) { DropPrevention(); }
		}
	}
	
	void BuildInputDetection()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		if(cmd.sidemove)
		{
			if(cmd.buttons & BT_MOVERIGHT)
				SVel += 40;
			else
				SVel -= 40;
				
			SVel = clamp(SVel, -90, 90);
		}
		else
		{
			SVel = 0;
		}
		
		if(cmd.forwardmove)
		{
			if(cmd.buttons & BT_FORWARD)
				FVel += 40;
			else
				FVel -= 40;
				
			FVel = clamp(FVel, -90, 90);
		}
		else
		{
			FVel = 0;
		}
		
		//Only for flying and swimming
		if(WaterLevel == 2 && DeepWater)
		{
			if(cmd.buttons & BT_JUMP)
			{
				if(Vel.Z > 0) { Vel.Z = 0.f; }
				UVel = 0;
				return;
			}
			else
			{
				DeepWater = False;
			}
		}
		
		if(bNOGRAVITY || WaterLevel >= 2)
		{
			if(cmd.buttons & BT_JUMP)
				UVel += 40;
			else if(cmd.buttons & BT_CROUCH)
				UVel -= 40;
			else
				UVel = 0;
				
			UVel = clamp(UVel, -90, 90);
		}
		else
		{
			UVel = 0;
		}
	}
	
	int BuildFriction()
	{
		//Friction Value
		Int Friction;
		if(WaterLevel >= 2)
			Friction = - 0x2000;
		else if(bNOGRAVITY)
			Friction = 0;
		else
			Friction = (8 - zm_friction) * 2 * 0x0200;
		
		//Applying the friction on the length of the vector instead of the single components fixed the Build engine directional distortion
		Int FrictionedVel = Int((!bNOGRAVITY && WaterLevel < 2 ? Vel.XY.Length() : Vel.Length()) * (0xCFD0 + Friction)) >> 16;
		
		return FrictionedVel <= 10.f ? 0 : FrictionedVel;
	}
	
	void BuildGroundMove()
	{
		//Values Reset
		MaxAirSpeed = Vel.XY.Length();
		
		//====================================
		//Actual Movement
		
		//Convert Velocity in Build Units
		Vel.XY *= 20.f;
		
		if(LandingVelZ < 10.f) //this value feels good
		{
			//Directional inputs
			BuildInputDetection();
			Acceleration.XY = RotateVector((FVel, - SVel), Angle);
			if(Acceleration.XY.Length()) { Acceleration.XY = min(Acceleration.XY.Length(), 90) * SafeUnit2(Acceleration.XY) * AccelMulti(); }
			
			//Acceleration
			Vel.XY += Acceleration.XY;
		}
		else //landing recovery
		{
			LandingVelZ--;
		}
		
		//Friction
		Int FrictionedVel = BuildFriction();
		
		//Translate back into Doom Units
		Vel.XY /= 20.f;
		FrictionedVel /= 20.f;
		
		//Limiter
		Vel.XY = min(FrictionedVel, MaxGroundSpeed) * SafeUnit2(Vel.XY);
		
		//Sprite Animation
		GroundSpriteAnimation();
	}
	
	void BuildAirMove()
	{
		Bool DashMove;
		if((DashNumber || Grappled) && MaxAirSpeed > MaxGroundSpeed) { DashMove = True; }
		
		//====================================
		//Actual Movement
		
		//Convert Velocity in Build Units
		Vel.XY *= 20.f;
		
		//Directional inputs
		BuildInputDetection();
		Acceleration.XY = RotateVector((FVel, - SVel), Angle);
		Acceleration.XY = min(Acceleration.XY.Length(), 90) * SafeUnit2(Acceleration.XY) * AccelMulti();
		
		//Acceleration
		Vel.XY += Acceleration.XY;
		
		//Friction
		Int FrictionedVel;
		if(!DashMove)
			FrictionedVel = BuildFriction();
		else
			FrictionedVel = Vel.XY.Length();
		
		//Limiter
		Vel.XY = min(FrictionedVel, (DashMove ? MaxAirSpeed : MaxGroundSpeed) * 20) * SafeUnit2(Vel.XY);
		
		//Translate back into Doom Units
		Vel.XY /= 20.f;
		
		//Sprite Animation
		AirSpriteAnimation();
		
		//Reset speed cap
		if(Vel.XY.Length() <= MaxGroundSpeed) { MaxAirSpeed = Vel.XY.Length(); }
	}
	
	void BuildWaterMove()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		//Value Resets
		MaxAirSpeed = Vel.XY.Length();
		
		//====================================
		//Actual Movement
		
		//Directional Inputs
		Acceleration = (cmd.forwardmove, - cmd.sidemove, 0);
		if(cmd.buttons & BT_JUMP || cmd.buttons & BT_CROUCH)
		{
			Acceleration.XY = (0, 0);
			Acceleration.Z = (cmd.buttons & BT_CROUCH ? -1 : 1) * (MaxGroundSpeed * 3.f / 40.f) * ActualSpeed;
		}
		else
		{
			Acceleration.XY = (MaxGroundSpeed * 3.f / 40.f) * SafeUnit2(Acceleration.XY);
			Acceleration.Z = Acceleration.X * sin(-Pitch);
			Acceleration.X *= cos(Pitch);
		}
		
		//Friction
		DFriction();
		if(WaterLevel == 2) { WaterVelZLimiter(); }
		
		//Acceleration
		Vel += (RotateVector(Acceleration.XY, Angle), Acceleration.Z);
		
		//Sprite Animation
		GroundSpriteAnimation();
	}
	
	void BuildFlyMove()
	{
		//Value Resets
		MaxAirSpeed = Vel.XY.Length();
		
		//====================================
		//Actual Movement
		
		//Convert Velocity in Build Units
		Vel *= 20.f;
		
		//Directional inputs
		BuildInputDetection();
		Acceleration.XY = RotateVector((FVel, - SVel), Angle);
		Acceleration.XY = min(Acceleration.XY.Length(), 90) * SafeUnit2(Acceleration.XY) * AccelMulti();
		
		//Acceleration
		Vel.XY += Acceleration.XY;
		
		//Crouch/jump press
		Vel.Z += UVel;
		
		//Friction
		Int FrictionedVel = BuildFriction();
		
		//Limiter
		Vel = min(FrictionedVel, MaxGroundSpeed * 30.f) * SafeUnit3(Vel);
		
		//Translate back into Doom Units
		Vel /= 20.f;
		
		// Sprite Animatiom
		PlayIdle();
	}
	
	void QuakeFriction(float StopSpeed, float Friction)
	{
		if(WaterLevel >= 2 || bNOGRAVITY)
		{
			if(Vel.Length() < 0.5f)
			{
				Vel.XY = (0, 0);
				return;
			}
		}
		else if(Vel.XY.Length() < 1.f)
		{
			Vel.XY = (0, 0);
			return;
		}
		
		if(FloorAngle >= 45 && ZMPlayer.OnGround) //lower friction on steep slopes
		{
			StopSpeed *= 4;
			Friction /= 4;
		}
		
		Double Velocity = Vel.Length();
		Double Drop, Control;
		if(WaterLevel >= 2)
		{
			drop = Velocity * Friction / TICRATE; //very tight friction
		}
		else if(bNOGRAVITY)
		{
			drop = Velocity * Friction / TICRATE; //loose friction
		}
		else if(ZMPlayer.OnGround)
		{
			if(!Pain)
			{
				Control = Velocity < StopSpeed ? zm_friction : Velocity;
				Drop = Control * Friction / TICRATE;
			}
		}
		
		Double NewVelocity = (Velocity - Drop <= 0 ? 0 : Velocity - Drop) / Velocity;
		if(WaterLevel >= 2 || bNOGRAVITY)
			Vel *= NewVelocity;
		else
			Vel.XY *= NewVelocity;
	}
	
	void QuakeAcceleration(Vector3 WishDir, Float WishSpeed, Float Accel)
	{
		Float CurrentSpeed = WishDir dot Vel;
		Float AddSpeed = WishSpeed - CurrentSpeed;
		if(AddSpeed <= 0) { return; }
		
		Float AccelerationSpeed = min(Accel * WishSpeed / TICRATE, AddSpeed);
		Vel += AccelerationSpeed * WishDir;
	}
	
	//////////////////////////////////////////
	// Crouching							//
	//////////////////////////////////////////
	
	//====================================
	// Regular crouching
	
	Override void CheckCrouch(bool totallyfrozen)
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		if(cmd.buttons & BT_JUMP)
		{
			cmd.buttons &= ~BT_CROUCH;
		}
		
		if(ZMPlayer.health > 0)
		{
			if(!totallyfrozen)
			{
				int crouchdir = ZMPlayer.crouching;
				
				if(bNOGRAVITY || WaterLevel >= 2) //forcefully uncrouch when flying/swimming
					crouchdir = 1;
				else if(crouchdir == 0)
					crouchdir = (cmd.buttons & BT_CROUCH) ? -1 : 1;
				else if(cmd.buttons & BT_CROUCH)
					ZMPlayer.crouching = 0;
				
				if(crouchdir == 1 && ZMPlayer.crouchfactor < 1 && pos.Z + height < ceilingz)
					CrouchMove(1);
				else if(crouchdir == -1 && ZMPlayer.crouchfactor > 0.5)
					CrouchMove(-1);
			}
		}
		else
		{
			ZMPlayer.Uncrouch();
		}

		ZMPlayer.crouchoffset = -(ViewHeight) * (1 - ZMPlayer.crouchfactor);
	}
	
	Override void CrouchMove(int direction)
	{
		double defaultheight = FullHeight;
		double savedheight = Height;
		double crouchspeed = (CanCSlide && zm_crouchslide == 1 ? - 1.5 : direction) * CROUCHSPEED;
		double oldheight = ZMPlayer.viewheight;

		ZMPlayer.crouchdir = direction;
		ZMPlayer.crouchfactor += crouchspeed;

		// check whether the move is ok
		Height  = defaultheight * ZMPlayer.crouchfactor;
		if(!TryMove(Pos.XY, false, NULL))
		{
			Height = savedheight;
			if(direction > 0)
			{
				// doesn't fit
				ZMPlayer.crouchfactor -= crouchspeed;
				return;
			}
		}
		Height = savedheight;

		ZMPlayer.crouchfactor = clamp(ZMPlayer.crouchfactor, 0.5, 1.);
		ZMPlayer.viewheight = ViewHeight * ZMPlayer.crouchfactor;
		ZMPlayer.crouchviewdelta = ZMPlayer.viewheight - ViewHeight;

		// Check for eyes going above/below fake floor due to crouching motion.
		CheckFakeFloorTriggers(pos.Z + oldheight, true);
	}
	
	//////////////////////////////////////
	// Bobbing							//
	//////////////////////////////////////
	
	void BobWeaponAuxiliary()
	{
		Usercmd cmd = ZMPlayer.cmd;
		
		Float Velocity = min(Vel.XY.Length(), zm_maxgroundspeed);
		Bool  InTheAirNoOffset = CanWSlide || CanCSlide || GrappleVel.Length() || bNOGRAVITY || WaterLevel >= 2;
		Bool  InTheAir = Jumped || abs(FloorZ - Pos.Z) > 16 || InTheAirNoOffset;
		
		//////////////////////////////////////////
		//Bobbing counter						//
		/////////////////////////////////////////
		
		DoBob = (cmd.forwardmove || cmd.sidemove) && Velocity > 1.f && !InTheAir && !VerticalOffset;
		if(DoBob || BobRange)
			BobTime += Velocity / zm_maxgroundspeed;
		else
			BobTime = 0;
		
		//////////////////////////////////////////
		//Horizontal sway and Vertical offset	//
		//////////////////////////////////////////
		
		Let PWeapon = ZMPlayer.ReadyWeapon;
		if(PWeapon == Null || PWeapon.bDontBob || !(ZMPlayer.WeaponState & WF_WEAPONBOBBING))
		{
			HorizontalSway = VerticalOffset = 0;
			return;
		}
		
		Let WeaponSprite = ZMPlayer.PSprites;
		if(WeaponSprite == Null) { return; }
		
		//=========================================
		//Horizontal Sway
		
		if(zm_sway)
		{
			HorizontalSway += (zm_swaydirection ? -1 : 1) *
						       ViewAngleDelta * zm_swayspeed / 10;
			Int SwayRange = zm_swayrange;
			HorizontalSway = clamp(HorizontalSway, -SwayRange, SwayRange);
			
			if(HorizontalSway)
			{
				Float Realign = min(abs(HorizontalSway), SwayRange - 1);
				HorizontalSway *= Realign / SwayRange * (abs(HorizontalSway) > BOB_MIN_REALIGN);
			}
			
			WeaponSprite.X = HorizontalSway;
		}
		
		//=========================================
		//Vertical offset
		
		if(zm_offset)
		{
			Int	OffsetRange = zm_offsetrange;
			
			if(InTheAir && !InTheAirNoOffset)
			{
				Float OffsetSpeed = zm_offsetspeed;
				
				if(!zm_offsetdirection)
				{
					if(Vel.Z >= 0)
						VerticalOffset += OffsetSpeed * Vel.Z * (1. - abs(VerticalOffset) / OffsetRange);
					else
						VerticalOffset += OffsetSpeed * Vel.Z * max(abs(VerticalOffset) / OffsetRange, 1);
				}
				else
				{
					if(Vel.Z >= 0)
						VerticalOffset -= OffsetSpeed * Vel.Z * max(abs(VerticalOffset) / OffsetRange, 1);
					else
						VerticalOffset -= OffsetSpeed * Vel.Z * (1. - abs(VerticalOffset) / OffsetRange);
				}
				VerticalOffset = clamp(VerticalOffset, 0, OffsetRange);
			}
			else if(VerticalOffset)
			{
				Float Realign = min(abs(VerticalOffset), OffsetRange - 1);
				VerticalOffset *= Realign / OffsetRange * (abs(VerticalOffset) > BOB_MIN_REALIGN);
			}
			
			WeaponSprite.Y = VerticalOffset + 32;
		}
	}
	
	void GetBobMulti(double ticfrac) //bobbing range and smooth transitioning
	{
		if(DoBob)
		{
			Double BobRangeCandidate = zm_maxgroundspeed * MoveFactor;
			if(BobRangeCandidate == BobRange) { return; }
			
			if(BobRangeCandidate > BobRange)
				BobRange = .95 * min(BobRange + abs(OldTicFrac - ticfrac) * abs(BobRangeCandidate - BobRange) / zm_maxgroundspeed, BobRangeCandidate); //make transitions proportional to frame time for fps consistency
			else																					
				BobRange = .95 * max(BobRange - abs(OldTicFrac - ticfrac) * abs(BobRangeCandidate - BobRange) / zm_maxgroundspeed, BobRangeCandidate); //and make the transition proportional to the value difference
		}																							
		else if(BobRange)
		{
			BobRange = max(BobRange - abs(OldTicFrac - ticfrac), 0);
		}
		OldTicFrac = ticfrac;
	}
	
	Override Vector2 BobWeapon(double ticfrac)
	{
		if(!ZMPlayer) { return (0, 0); }
		
		let weapon = ZMPlayer.ReadyWeapon;
		if(weapon == null) { return (0, 0); }
		
		Vector2 r;
		GetBobMulti(ticfrac);
		int bobstyle = weapon.BobStyle;
		double RangeY = weapon.BobRangeY;
		if(weapon.bDontBob || !BobRange || !(ZMPlayer.WeaponState & WF_WEAPONBOBBING)) //I should add a variable to do this very cleanly but I don't wanna
		{
			BobRange = BobTime = 0;
			switch(bobstyle)
			{
			case Bob_Dusk:
				r.Y = zm_maxgroundspeed * RangeY;
				break;
				
			case Bob_Painkiller:
				r.Y = zm_maxgroundspeed * RangeY;
				break;
					
			case Bob_UT:
				r.Y = zm_maxgroundspeed * RangeY;
			}
			return r;
		}
		
		double BobSpeed = weapon.BobSpeed * 150;//128;
		double bobx = weapon.BobRangeX * BobRange;
		double boby = weapon.BobRangeY * BobRange;
		Vector2 p1, p2;
		
		for(int i = 0; i < 2; i++)
		{
			double BobAngle = BobSpeed * ZMPlayer.GetWBobSpeed() * (BobTime + i - 1) * (360. / 8192.);
			
			switch(bobstyle)
			{
			case Bob_Normal:
				r.X = bobx * cos(BobAngle);
				r.Y = boby * abs(sin(BobAngle));
				break;
			
			case Bob_Inverse:
				r.X = bobx * cos(BobAngle);
				r.Y = boby * (1. - abs(sin(BobAngle)));
				break;
				
			case Bob_Alpha:
				r.X = bobx * sin(BobAngle);
				r.Y = boby * abs(sin(BobAngle));
				break;
			
			case Bob_InverseAlpha:
				r.X = bobx * sin(BobAngle);
				r.Y = boby * (1. - abs(sin(BobAngle)));
				break;
			
			case Bob_Smooth:
				r.X = bobx * cos(BobAngle);
				r.Y = boby * (1. - (cos(BobAngle * 2))) / 2.f;
				break;
			
			case Bob_InverseSmooth:
				r.X = bobx * cos(BobAngle);
				r.Y = boby * (1. + (cos(BobAngle * 2))) / 2.f;
			
			case Bob_Build:
				r.X = 2. * bobx * cos(BobAngle);	
				r.Y = boby * (1. - abs(sin(BobAngle)));
				break;
			
			case Bob_Dusk:
				r.X = bobx * cos((BobAngle * 2.) / 3.);
				r.Y = boby * (cos(2.2 * BobAngle)) + zm_maxgroundspeed * RangeY;
				break;
			
			case Bob_Painkiller:
				r.X = bobx * cos(BobAngle);	
				r.Y = - boby * (1. - abs(sin(BobAngle))) + zm_maxgroundspeed * RangeY;
				break;
							
			case Bob_UT:
				r.X = 1.5 * bobx * cos(BobAngle);	
				r.Y = boby * sin(2. * BobAngle) + zm_maxgroundspeed * RangeY;
			}
			
			if (i == 0) p1 = r; else p2 = r;
		}
		
		return p1 * (1. - ticfrac) + p2 * ticfrac;
	}
}



Class UEM_LedgeGrabWeapon : Weapon
{
	States
	{
		Select:
			TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			TNT1 A 0 A_Raise;
			Loop;
			
		Deselect:
			TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			TNT1 A 0 A_Lower;
			Loop;
			
		Ready:
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			Loop;
			
		Fire:
			TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			Stop;
	}
}